defmodule Serum.Project.ElixirValidatorTest do
  @moduledoc """
  Tests for `Serum.Project.ElixirValidator`

  The purpose of this module is to check whether the input (an Elixir term
  generated by evaluating `serum.exs`) is a valid Serum project definition.

  An Elixir term `t` is a valid Serum project definition if all of the
  conditions below is true:

  - `t` is a map.
  - `t` contains all the required keys.
  - `t` does not contain any undefined key, which is neither of the required
    keys nor the optional keys.
  - Any value associated with a key in `t` does not violate the constraints
    specific to that key.
  """

  use ExUnit.Case
  import Serum.Project.ElixirValidator

  @test_path "/test/serum.exs"

  @base_map %{
    site_name: "Test Site",
    site_description: "This is a test site.",
    author: "John Doe",
    author_email: "john.doe@example.com",
    base_url: "/"
  }

  describe "validate/2" do
    test "good, contains required keys only" do
      assert :ok = validate(@base_map, @test_path)
    end

    test "good, also contains optional keys" do
      map =
        Map.merge(@base_map, %{
          server_root: "https://example.com/test",
          date_format: "{WDfull}, {D} {Mshort} {YYYY}",
          list_title_all: "All posts",
          list_title_tag: "Posts about ~s",
          pagination: true,
          posts_per_page: 10,
          preview_length: 200
        })

      assert :ok = validate(map, @test_path)
    end

    test "not a map" do
      assert {:error, _} = validate(:foo, @test_path)
    end

    test "missing one required key" do
      map = Map.delete(@base_map, :base_url)
      {:error, {msg, @test_path, _}} = validate(map, @test_path)

      assert String.starts_with?(msg, "missing required property:")
    end

    test "missing multiple required keys" do
      map = Map.drop(@base_map, [:base_url, :site_description])
      {:error, {msg, @test_path, _}} = validate(map, @test_path)

      assert String.starts_with?(msg, "missing required properties:")
    end

    test "one extra key" do
      map = Map.put(@base_map, :foo, :bar)
      {:error, {msg, @test_path, _}} = validate(map, @test_path)

      assert String.starts_with?(msg, "unknown property:")
    end

    test "multiple extra keys" do
      map =
        @base_map
        |> Map.put(:foo, :bar)
        |> Map.put(:lorem, "ipsum")

      {:error, {msg, @test_path, _}} = validate(map, @test_path)

      assert String.starts_with?(msg, "unknown properties:")
    end

    test "constraint check 1" do
      map = Map.put(@base_map, :base_url, "hello")
      {:error, {_, l}} = validate(map, @test_path)

      assert length(l) == 1
    end

    test "constraint check 2" do
      map =
        Map.merge(@base_map, %{
          base_url: "hello",
          server_root: "htttps://foo.bar/baz",
          date_format: 3,
          posts_per_page: 0,
          preview_length: -1
        })

      {:error, {_, l}} = validate(map, @test_path)

      assert length(l) == 5
    end
  end
end
